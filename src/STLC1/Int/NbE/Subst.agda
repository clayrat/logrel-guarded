module STLC1.Int.NbE.Subst where

open import Prelude hiding ([_])
open import Data.Empty
open import Data.Dec

open import STLC1.Int.TyTerm
open import STLC1.Int.NbE.CtxExt

infixr 5 _â†¥âŠ¢_
infixr 5 _â‰¤âŠ¢_
infix  8 _[_]Ê³
infix  8 _[_]
infix  8 _âˆ·Ë¢_
infixr 9 _âˆ˜Ë¢_
infixr 9 _âˆ˜Ê³_

Sub : Ctx â†’ Ctx â†’ ğ’°
Sub Î“ Î” = âˆ€ {T : Ty} â†’ Î” âˆ‹ T â†’ Î“ âŠ¢ T

Ren : Ctx â†’ Ctx â†’ ğ’°
Ren Î“ Î” = âˆ€ {T : Ty} â†’ Î” âˆ‹ T â†’ Î“ âˆ‹ T

ren : âˆ€ {Î“ Î” : Ctx} â†’ Ren Î“ Î” â†’ Sub Î“ Î”
ren Ï x = ` Ï x

idÊ³ : âˆ€ {Î“ : Ctx} â†’ Ren Î“ Î“
idÊ³ = id

â†¥Ê³ : âˆ€ {Î“ : Ctx} {T : Ty} â†’ Ren (Î“ ï¹ T) Î“
â†¥Ê³ = there

_âˆ˜Ê³_ : âˆ€ {Î“ Î” Î£ : Ctx} â†’ Ren Î” Î“ â†’ Ren Î£ Î” â†’ Ren Î£ Î“
Ï âˆ˜Ê³ Ï‰ = Ï‰ âˆ˜ Ï

Ï-â‰¤ : âˆ€ {Î“ Î” : Ctx} â†’ Î“ â‰¤ Î” â†’ Ren Î“ Î”
Ï-â‰¤ {Î“} (â‰¤-id  e)  {T} = subst (Ren Î“) e idÊ³
Ï-â‰¤     (â‰¤-ext pf)     = Ï-â‰¤ pf âˆ˜Ê³ â†¥Ê³

ext : âˆ€ {Î“ Î” : Ctx} â†’ Ren Î“ Î” â†’ âˆ€ {T : Ty} â†’ Ren (Î“ ï¹ T) (Î” ï¹ T)
ext Ï  here     = here
ext Ï (there x) = there (Ï x)

_[_]Ê³ : âˆ€ {Î“ Î” : Ctx} {T : Ty}
      â†’ Î” âŠ¢ T
      â†’ Ren Î“ Î”
        -------
      â†’ Î“ âŠ¢ T
ğ“‰ğ“‰      [ Ï ]Ê³ = ğ“‰ğ“‰
` x     [ Ï ]Ê³ = ` Ï x
(Æ› t)   [ Ï ]Ê³ = Æ› (t [ ext Ï ]Ê³)
(r Â· s) [ Ï ]Ê³ = (r [ Ï ]Ê³) Â· (s [ Ï ]Ê³)

exts : âˆ€ {Î“ Î” : Ctx} â†’ Sub Î“ Î” â†’ âˆ€ {T : Ty} â†’ Sub (Î“ ï¹ T) (Î” ï¹ T)
exts Ïƒ  here     = ` here
exts Ïƒ (there x) = (Ïƒ x) [ â†¥Ê³ ]Ê³

_[_] : âˆ€ {Î“ Î” : Ctx} {T : Ty}
     â†’ Î” âŠ¢ T
     â†’ Sub Î“ Î”
       -------
     â†’ Î“ âŠ¢ T
ğ“‰ğ“‰      [ Ïƒ ] = ğ“‰ğ“‰
(` x)   [ Ïƒ ] = Ïƒ x
(Æ› t)   [ Ïƒ ] = Æ› t [ exts Ïƒ ]
(r Â· s) [ Ïƒ ] = (r [ Ïƒ ]) Â· (s [ Ïƒ ])

idË¢ : âˆ€ {Î“ : Ctx} â†’ Sub Î“ Î“
idË¢ = `_

â†¥ : âˆ€ {Î“ : Ctx} {T : Ty} â†’ Sub (Î“ ï¹ T) Î“
â†¥ x = ` there x

_âˆ˜Ë¢_ : âˆ€ {Î“ Î” Î£ : Ctx} â†’ Sub Î” Î“ â†’ Sub Î£ Î” â†’ Sub Î£ Î“
(Ïƒ âˆ˜Ë¢ Ï„) x = (Ïƒ x) [ Ï„ ]

_âˆ·Ë¢_ : âˆ€ {Î“ Î” : Ctx} {S : Ty} â†’ Sub Î“ Î” â†’ Î“ âŠ¢ S â†’ Sub Î“ (Î” ï¹ S)
(_ âˆ·Ë¢ s)  here     = s
(Ïƒ âˆ·Ë¢ _) (there x) = Ïƒ x

weaken : âˆ€ {Î“ Î” : Ctx}
       â†’ Î“ â‰¤ Î”
         --------
       â†’ Sub Î“ Î”
weaken pf x = ` Ï-â‰¤ pf x

_â‰¤âŠ¢_ : âˆ€ {Î“ Î” : Ctx} {T : Ty}
      â†’ Î“ â‰¤ Î” â†’ Î” âŠ¢ T â†’ Î“ âŠ¢ T
pf â‰¤âŠ¢ t = t [ weaken pf ]

_â†¥âŠ¢_ : âˆ€ {Î“ : Ctx} {T : Ty} â†’ (S : Ty) â†’ Î“ âŠ¢ T â†’ Î“ ï¹ S âŠ¢ T
_ â†¥âŠ¢ t = t [ â†¥ ]

sub-tail : âˆ€ {Î“ Î” : Ctx} {S T : Ty} {s : Î“ âŠ¢ S} {Ïƒ : Sub Î“ Î”}
         â†’ (â†¥ âˆ˜Ë¢ (Ïƒ âˆ·Ë¢ s)) {T = T} ï¼ Ïƒ
sub-tail = refl

sub-dist : âˆ€ {Î“ Î” Î£ : Ctx} {S T : Ty} {Ï„ : Sub Î“ Î”} {Ïƒ : Sub Î” Î£} {s : Î” âŠ¢ S}
         â†’ (Ïƒ âˆ·Ë¢ s) âˆ˜Ë¢ Ï„ ï¼ (Ïƒ âˆ˜Ë¢ Ï„ âˆ·Ë¢ (s [ Ï„ ])) {T}
sub-dist {Î£} {S} {T} {Ï„} {Ïƒ} {s} = fun-ext go
  where
  go : (x : Î£ ï¹ S âˆ‹ T) â†’ ((Ïƒ âˆ·Ë¢ s) âˆ˜Ë¢ Ï„) x ï¼ (Ïƒ âˆ˜Ë¢ Ï„ âˆ·Ë¢ (s [ Ï„ ])) x
  go  here     = refl
  go (there x) = refl

cong-ext : âˆ€ {Î“ Î” : Ctx} {Ï Ïâ€² : Ren Î“ Î”} {T : Ty}
         â†’ (âˆ€ {S : Ty} â†’ Ï ï¼ Ïâ€² {S})
         â†’ âˆ€ {S : Ty} â†’ ext Ï {T} {S} ï¼ ext Ïâ€²
cong-ext {Î”} {Ï} {Ïâ€²} {T} Ïï¼Ïâ€² {S} = fun-ext go
  where
  go : (x : Î” ï¹ T âˆ‹ S) â†’ ext Ï x ï¼ ext Ïâ€² x
  go  here     = refl
  go (there x) = ap there (happly Ïï¼Ïâ€² x)

cong-rename : âˆ€ {Î“ Î” : Ctx} {Ï Ïâ€² : Ren Î“ Î”} {T : Ty} {t : Î” âŠ¢ T}
            â†’ (âˆ€ {S : Ty} â†’ Ï ï¼ Ïâ€² {S})
            â†’ t [ Ï ]Ê³ ï¼ t [ Ïâ€² ]Ê³
cong-rename {t = ğ“‰ğ“‰}    Ïï¼Ïâ€² = refl
cong-rename {t = ` x}   Ïï¼Ïâ€² = ap `_ (happly Ïï¼Ïâ€² x)
cong-rename {t = Æ› t}   Ïï¼Ïâ€² = ap Æ›_ (cong-rename {t = t} (cong-ext Ïï¼Ïâ€²))
cong-rename {t = r Â· s} Ïï¼Ïâ€² = apÂ² _Â·_ (cong-rename {t = r} Ïï¼Ïâ€²) (cong-rename {t = s} Ïï¼Ïâ€²)

cong-exts : âˆ€ {Î“ Î” : Ctx} {Ïƒ Ïƒâ€² : Sub Î“ Î”} {T : Ty}
          â†’ (âˆ€ {S : Ty} â†’ Ïƒ ï¼ Ïƒâ€² {S})
          â†’ (âˆ€ {S : Ty} â†’ exts Ïƒ {T} {S} ï¼ exts Ïƒâ€²)
cong-exts {Î”} {Ïƒ} {Ïƒâ€²} {T} Ïƒï¼Ïƒâ€² {S} = fun-ext go
  where
  go : (x : Î” ï¹ T âˆ‹ S) â†’ exts Ïƒ x ï¼ exts Ïƒâ€² x
  go  here     = refl
  go (there x) = ap (_[ there ]Ê³) (happly Ïƒï¼Ïƒâ€² x)

cong-sub : âˆ€ {Î“ Î” : Ctx} {Ïƒ Ïƒâ€² : Sub Î“ Î”} {T : Ty} {t tâ€² : Î” âŠ¢ T}
         â†’ (âˆ€ {S : Ty} â†’ Ïƒ ï¼ Ïƒâ€² {S})
         â†’ t ï¼ tâ€²
         â†’ t [ Ïƒ ] ï¼ tâ€² [ Ïƒâ€² ]
cong-sub      {t = ğ“‰ğ“‰} {tâ€² = ğ“‰ğ“‰}    Ïƒï¼Ïƒâ€² tï¼tâ€² = refl
cong-sub      {t = ğ“‰ğ“‰} {tâ€² = ` _}   Ïƒï¼Ïƒâ€² tï¼tâ€² = absurd (ğ“‰ğ“‰â‰ ` tï¼tâ€²)
cong-sub      {t = ğ“‰ğ“‰} {tâ€² = _ Â· _} Ïƒï¼Ïƒâ€² tï¼tâ€² = absurd (ğ“‰ğ“‰â‰ Â· tï¼tâ€²)
cong-sub {Ïƒâ€²} {t = ` x}             Ïƒï¼Ïƒâ€² tï¼tâ€² = happly Ïƒï¼Ïƒâ€² x âˆ™ ap _[ Ïƒâ€² ] tï¼tâ€²
cong-sub {Ïƒâ€²} {t = Æ› t}             Ïƒï¼Ïƒâ€² tï¼tâ€² =
    ap Æ›_ (cong-sub  {Ïƒâ€² = exts Ïƒâ€²} {t = t} (cong-exts Ïƒï¼Ïƒâ€²) refl)
  âˆ™ ap (_[ Ïƒâ€² ]) tï¼tâ€²
cong-sub {Ïƒâ€²} {t = r Â· s}           Ïƒï¼Ïƒâ€² tï¼tâ€² =
    apÂ² _Â·_ (cong-sub {t = r} Ïƒï¼Ïƒâ€² refl) (cong-sub {t = s} Ïƒï¼Ïƒâ€² refl)
  âˆ™ ap (_[ Ïƒâ€² ]) tï¼tâ€²

cong-cons : âˆ€ {Î“ Î” : Ctx} {S : Ty} {s sâ€² : Î“ âŠ¢ S} {Ïƒ Ï„ : Sub Î“ Î”}
          â†’ s ï¼ sâ€²
          â†’ (âˆ€ {T : Ty} â†’ Ïƒ {T} ï¼ Ï„ {T})
          â†’ âˆ€ {T : Ty} â†’ Ïƒ âˆ·Ë¢ s ï¼ (Ï„ âˆ·Ë¢ sâ€²) {T}
cong-cons {Î”} {S} {s} {sâ€²} {Ïƒ} {Ï„} sï¼sâ€² Ïƒï¼Ï„ {T} = fun-ext go
  where
  go : (x : Î” ï¹ S âˆ‹ T) â†’ (Ïƒ âˆ·Ë¢ s) x ï¼ (Ï„ âˆ·Ë¢ sâ€²) x
  go  here     = sï¼sâ€²
  go (there x) = happly Ïƒï¼Ï„ x

cong-seq : âˆ€ {Î“ Î” Î£ : Ctx} {Ï„ Ï„â€² : Sub Î“ Î”} {Ïƒ Ïƒâ€² : Sub Î” Î£}
         â†’ (âˆ€ {T : Ty} â†’ Ïƒ {T} ï¼ Ïƒâ€²)
         â†’ (âˆ€ {T : Ty} â†’ Ï„ {T} ï¼ Ï„â€²)
         â†’ (âˆ€ {T : Ty} â†’ (Ïƒ âˆ˜Ë¢ Ï„) {T} ï¼ Ïƒâ€² âˆ˜Ë¢ Ï„â€²)
cong-seq {Î£} {Ï„} {Ï„â€²} {Ïƒ} {Ïƒâ€²} Ïƒï¼Ïƒâ€² Ï„ï¼Ï„â€² {T} =
  fun-ext (Î» x â†’ cong-sub {Ïƒ = Ï„} {Ïƒâ€² = Ï„â€²} {t = Ïƒ x} {tâ€² = Ïƒâ€² x} Ï„ï¼Ï„â€² (happly Ïƒï¼Ïƒâ€² x))

ren-ext : âˆ€ {Î“ Î” : Ctx} {S T : Ty} {Ï : Ren Î“ Î”}
        â†’ ren (ext Ï) ï¼ exts (ren Ï) {S} {T}
ren-ext {Î”} {S} {T} {Ï} = fun-ext go
  where
  go : (x : Î” ï¹ S âˆ‹ T) â†’ (ren (ext Ï)) x ï¼ exts (ren Ï) x
  go  here     = refl
  go (there x) = refl

rename-subst-ren : âˆ€ {Î“ Î” : Ctx} {T : Ty} {Ï : Ren Î“ Î”} {t : Î” âŠ¢ T}
                 â†’ t [ Ï ]Ê³ ï¼ t [ ren Ï ]
rename-subst-ren {t = ğ“‰ğ“‰}    = refl
rename-subst-ren {t = ` x}   = refl
rename-subst-ren {Ï} {t = Æ› t} =
  ap Æ›_ (rename-subst-ren {Ï = ext Ï} {t}
         âˆ™ cong-sub {t = t} ren-ext refl)
rename-subst-ren {t = r Â· s} =
  apÂ² _Â·_ rename-subst-ren rename-subst-ren

ren-shift : âˆ€ {Î“ : Ctx} {S T : Ty}
          â†’ ren â†¥Ê³ ï¼ â†¥ {Î“} {S} {T}
ren-shift {Î“} {T} = fun-ext go
  where
  go : (x : Î“ âˆ‹ T) â†’ ren â†¥Ê³ x ï¼ â†¥ x
  go  here     = refl
  go (there x) = refl

rename-shift : âˆ€ {Î“ : Ctx} {S T : Ty} {s : Î“ âŠ¢ S}
             â†’ s [ â†¥Ê³ {T = T} ]Ê³ ï¼ s [ â†¥ ]
rename-shift {Î“} {S} {T} {s} = rename-subst-ren

exts-cons-shift : âˆ€ {Î“ Î” : Ctx} {S T : Ty} {Ïƒ : Sub Î“ Î”}
                â†’ exts Ïƒ {S} {T} ï¼ (Ïƒ âˆ˜Ë¢ â†¥) âˆ·Ë¢ ` here
exts-cons-shift {Î”} {S} {T} {Ïƒ} = fun-ext go
  where
  go : (x : Î” ï¹ S âˆ‹ T) â†’ exts Ïƒ x ï¼ ((Ïƒ âˆ˜Ë¢ â†¥) âˆ·Ë¢ ` here) x
  go  here     = refl
  go (there x) = rename-subst-ren

exts-idË¢ : âˆ€ {Î“ : Ctx} {S T : Ty}
         â†’ exts idË¢ ï¼ idË¢ {Î“ ï¹ S} {T}
exts-idË¢ {Î“} {S} {T} = fun-ext go
  where
  go : (x : Î“ ï¹ S âˆ‹ T) â†’ exts idË¢ x ï¼ idË¢ x
  go  here     = refl
  go (there x) = refl

[id]-identity : âˆ€ {Î“ : Ctx} {T : Ty} {t : Î“ âŠ¢ T}
              â†’ t [ idË¢ ] ï¼ t
[id]-identity {t = ğ“‰ğ“‰}    = refl
[id]-identity {t = ` x}   = refl
[id]-identity {t = Æ› t}   = ap Æ›_ (cong-sub {t = t} exts-idË¢ refl âˆ™ [id]-identity)
[id]-identity {t = r Â· s} = apÂ² _Â·_ [id]-identity [id]-identity

sub-idR : âˆ€ {Î“ Î” : Ctx} {Ïƒ : Sub Î“ Î”} {T : Ty}
        â†’ (Ïƒ âˆ˜Ë¢ idË¢) ï¼ Ïƒ {T}
sub-idR = fun-ext Î» _ â†’ [id]-identity

compose-ext : âˆ€ {Î“ Î” Î£ : Ctx} {Ï : Ren Î” Î£} {Ï‰ : Ren Î“ Î”} {S T : Ty}
            â†’ (ext Ï) âˆ˜Ê³ (ext Ï‰) ï¼ ext (Ï âˆ˜Ê³ Ï‰) {S} {T}
compose-ext {Î£} {Ï} {Ï‰} {S} {T} = fun-ext go where
  go : (x : Î£ ï¹ S âˆ‹ T) â†’ ((ext Ï) âˆ˜Ê³ (ext Ï‰)) x ï¼ ext (Ï âˆ˜Ê³ Ï‰) x
  go  here     = refl
  go (there x) = refl

compose-rename : âˆ€ {Î“ Î” Î£ : Ctx} {T : Ty} {t : Î£ âŠ¢ T} {Ï‰ : Ren Î“ Î”}
                   {Ï : Ren Î” Î£}
               â†’ t [ Ï ]Ê³ [ Ï‰ ]Ê³ ï¼ t [ Ï âˆ˜Ê³ Ï‰ ]Ê³
compose-rename {t = ğ“‰ğ“‰}    = refl
compose-rename {t = ` x}   = refl
compose-rename {t = Æ› t}   = ap Æ›_ (compose-rename {t = t} âˆ™ cong-rename compose-ext)
compose-rename {t = r Â· s} = apÂ² _Â·_ compose-rename compose-rename

commute-subst-rename : âˆ€ {Î“ Î” Î£ Î¦ : Ctx} {T : Ty} {t : Î£ âŠ¢ T}
                         {Ïƒ : Sub Î“ Î”} {Ï : Ren Î” Î£}
                         {Ïâ€² : Ren Î“ Î¦ } {Ïƒâ€² : Sub Î¦ Î£}
                     â†’ (âˆ€ {S : Ty} {x : Î£ âˆ‹ S} â†’ Ïƒ (Ï x) ï¼ Ïƒâ€² x [ Ïâ€² ]Ê³)
                     â†’ t [ Ï ]Ê³ [ Ïƒ ] ï¼ t [ Ïƒâ€² ] [ Ïâ€² ]Ê³
commute-subst-rename                 {t = ğ“‰ğ“‰}                      pf = refl
commute-subst-rename                 {t = ` x}                     pf = pf
commute-subst-rename {Î£} {T = S â‡’ T} {t = Æ› t}   {Ïƒ} {Ï} {Ïâ€²} {Ïƒâ€²} pf =
  ap Æ›_ (commute-subst-rename {t = t} (Î» {U} {x} â†’ go x))
  where
  go : âˆ€ {U : Ty} (x : Î£ ï¹ S âˆ‹ U) â†’ exts Ïƒ (ext Ï x) ï¼ exts Ïƒâ€² x [ ext Ïâ€² ]Ê³
  go  here     = refl
  go (there x) =   ap (_[ â†¥Ê³ ]Ê³) pf
                 âˆ™ compose-rename {t = Ïƒâ€² x} {â†¥Ê³ {T = S}} {Ïâ€²}
                 âˆ™ sym (compose-rename {t = Ïƒâ€² x} {ext Ïâ€² {S}} {â†¥Ê³})
commute-subst-rename                 {t = r Â· s} {Ïƒ} {Ï} {Ïâ€²} {Ïƒâ€²} pf =
  apÂ² _Â·_ (commute-subst-rename {t = r} {Ïƒ} {Ï} {Ïâ€²} {Ïƒâ€²} pf)
          (commute-subst-rename {t = s} {Ïƒ} {Ï} {Ïâ€²} {Ïƒâ€²} pf)

sub-there-shift : âˆ€ {Î“ Î” : Ctx} {S T : Ty} {Ïƒ : Sub Î“ (Î” ï¹ S)} {x : Î” âˆ‹ T}
                â†’ Ïƒ (there x) ï¼ (â†¥ âˆ˜Ë¢ Ïƒ) x
sub-there-shift = refl

exts-seq : âˆ€ {Î“ Î” Î£ : Ctx} {Ï„ : Sub Î“ Î”} {Ïƒ : Sub Î” Î£} {S : Ty}
         â†’ âˆ€ {T : Ty} â†’ (exts Ïƒ âˆ˜Ë¢ exts Ï„) ï¼ exts (Ïƒ âˆ˜Ë¢ Ï„) {S} {T}
exts-seq {Î£} {Ï„} {Ïƒ} {S} {T} = fun-ext go
  where
  go : (x : Î£ ï¹ S âˆ‹ T) â†’ (exts Ïƒ âˆ˜Ë¢ exts Ï„) x ï¼ exts (Ïƒ âˆ˜Ë¢ Ï„) x
  go  here     = refl
  go (there x) =   sub-there-shift {Ïƒ = exts Ïƒ âˆ˜Ë¢ exts Ï„} {x = x}
                 âˆ™ commute-subst-rename {t = Ïƒ x} refl

sub-sub : âˆ€ {Î“ Î” Î£ : Ctx} {T : Ty} {Ï„ : Sub Î“ Î”} {Ïƒ : Sub Î” Î£} {t : Î£ âŠ¢ T}
        â†’ t [ Ïƒ ] [ Ï„ ] ï¼ t [ Ïƒ âˆ˜Ë¢ Ï„ ]
sub-sub         {t = ğ“‰ğ“‰}    = refl
sub-sub         {t = ` x}   = refl
sub-sub {Ï„} {Ïƒ} {t = Æ› t}   = ap Æ›_ (  sub-sub {Ï„ = exts Ï„} {exts Ïƒ} {t}
                                     âˆ™ cong-sub {t = t} exts-seq refl)
sub-sub         {t = r Â· s} = apÂ² _Â·_ (sub-sub {t = r}) (sub-sub {t = s})

sub-assoc : âˆ€ {Î“ Î” Î£ Î¦ : Ctx} {Ïƒ : Sub Î” Î“} {Ï„ : Sub Î£ Î”} {Î¸ : Sub Î¦ Î£}
          â†’ âˆ€ {T : Ty} â†’ (Ïƒ âˆ˜Ë¢ Ï„) âˆ˜Ë¢ Î¸ ï¼ (Ïƒ âˆ˜Ë¢ Ï„ âˆ˜Ë¢ Î¸) {T}
sub-assoc {Ïƒ} {Ï„} {Î¸} = fun-ext Î» x â†’ sub-sub {Ï„ = Î¸} {Ï„} {t = Ïƒ x}

subst-zero-exts-cons : âˆ€ {Î“ Î” : Ctx} {S T : Ty} {Ïƒ : Sub Î“ Î”} {s : Î“ âŠ¢ S}
                     â†’ exts Ïƒ âˆ˜Ë¢ (idË¢ âˆ·Ë¢ s) ï¼ (Ïƒ âˆ·Ë¢ s) {T}
subst-zero-exts-cons {S = S} {T} {Ïƒ} {s} =
  exts Ïƒ âˆ˜Ë¢ (idË¢ âˆ·Ë¢ s)
    ï¼âŸ¨ cong-seq exts-cons-shift refl âŸ©
  ((Ïƒ âˆ˜Ë¢ â†¥) âˆ·Ë¢ ` here) âˆ˜Ë¢ (idË¢ âˆ·Ë¢ s)
    ï¼âŸ¨ sub-dist âŸ©
  ((Ïƒ âˆ˜Ë¢ â†¥) âˆ˜Ë¢ (idË¢ âˆ·Ë¢ s)) âˆ·Ë¢ s
    ï¼âŸ¨ cong-cons refl (sub-assoc {Ïƒ = Ïƒ}) âŸ©
  (Ïƒ âˆ˜Ë¢ â†¥ âˆ˜Ë¢ (idË¢ âˆ·Ë¢ s)) âˆ·Ë¢ s
    ï¼âŸ¨ cong-cons refl (cong-seq {Ïƒ = Ïƒ} refl (sub-tail {s = s} {Ïƒ = idË¢})) âŸ©
  (Ïƒ âˆ˜Ë¢ idË¢) âˆ·Ë¢ s
    ï¼âŸ¨ cong-cons refl (sub-idR {Ïƒ = Ïƒ}) âŸ©
  Ïƒ âˆ·Ë¢ s
    âˆ

compose-Ï-â‰¤ : âˆ€ {Î“â€³ Î“â€² Î“ : Ctx} {T : Ty}
            â†’ (Î“â€³â‰¤Î“â€² : Î“â€³ â‰¤ Î“â€²)
            â†’ (Î“â€²â‰¤Î“ : Î“â€² â‰¤ Î“)
            â†’ (x : Î“ âˆ‹ T)
            â†’ Ï-â‰¤ Î“â€³â‰¤Î“â€² (Ï-â‰¤ Î“â€²â‰¤Î“ x) ï¼ Ï-â‰¤ (â‰¤-trans Î“â€³â‰¤Î“â€² Î“â€²â‰¤Î“) x
compose-Ï-â‰¤ {Î“â€³}          {Î“â€²}              {T} (â‰¤-id eâ€³)                    (â‰¤-id eâ€²)                    x =
  JÂ² (Î» Î“â‚ eâ‚ Î“â‚€ eâ‚€ â†’ (xâ‚€ : Î“â‚€ âˆ‹ T) â†’
        subst (Ren Î“â€³) eâ‚ idÊ³ (subst (Ren Î“â‚) eâ‚€ idÊ³ xâ‚€) ï¼ subst (Ren Î“â€³) (eâ‚ âˆ™ eâ‚€) idÊ³ xâ‚€)
     (Î» xâ‚€ â†’   ap (Î» q â†’ subst (Ren Î“â€³) refl idÊ³ (q xâ‚€)) (subst-refl {B = Ren Î“â€³} idÊ³)
             âˆ™ ap (Î» q â†’ q xâ‚€) (subst-refl {B = Ren Î“â€³} idÊ³)
             âˆ™ ap (Î» q â†’ q xâ‚€) (sym (subst-refl {B = Ren Î“â€³} idÊ³))
             âˆ™ ap (Î» q â†’ subst (Ren Î“â€³) q idÊ³ xâ‚€) (sym (âˆ™-id-r refl)))
     eâ€³ eâ€² x
compose-Ï-â‰¤ {Î“â€³}          {.(Î“â€² ï¹ Tâ€²)} {Î“}     (â‰¤-id e)                     (â‰¤-ext {Î“ = Î“â€²} {T = Tâ€²} pf) x =
  J (Î» Î“â‚‚ eâ‚‚ â†’
       subst (Ren Î“â‚‚) (sym eâ‚‚) idÊ³ ((Ï-â‰¤ pf âˆ˜Ê³ â†¥Ê³) x) ï¼ Ï-â‰¤ (subst (_â‰¤ Î“) eâ‚‚ (â‰¤-ext pf)) x)
    (  ap (Î» q â†’ q ((Ï-â‰¤ pf âˆ˜Ê³ â†¥Ê³) x)) (subst-refl {B = Ren (Î“â€² ï¹ Tâ€²)} idÊ³)
     âˆ™ ap (Î» q â†’ Ï-â‰¤ q x) (sym (subst-refl {B = _â‰¤ Î“} (â‰¤-ext pf))))
    (sym e)
compose-Ï-â‰¤ {.(Î“â€³ ï¹ Tâ€³)} {Î“â€²}          {Î“} {T} (â‰¤-ext {Î“ = Î“â€³} {T = Tâ€³} pf) (â‰¤-id e)                     x =
  J (Î» Î“â‚€ eâ‚€ â†’ (xâ‚€ : Î“â‚€ âˆ‹ T) â†’
       (Ï-â‰¤ pf âˆ˜Ê³ â†¥Ê³) (subst (Ren Î“â€²) eâ‚€ idÊ³ xâ‚€) ï¼ Ï-â‰¤ (subst (Î“â€³ ï¹ Tâ€³ â‰¤_) eâ‚€ (â‰¤-ext pf)) xâ‚€)
    (Î» xâ‚€ â†’   ap (Î» q â†’ (Ï-â‰¤ pf âˆ˜Ê³ â†¥Ê³ {T = Tâ€³}) (q xâ‚€)) (subst-refl {B = Ren Î“â€²} idÊ³)
            âˆ™ ap (Î» q â†’ Ï-â‰¤ q xâ‚€) (sym (subst-refl {B = Î“â€³ ï¹ Tâ€³ â‰¤_} (â‰¤-ext pf))))
    e x
compose-Ï-â‰¤                                     (â‰¤-ext pf1)                  (â‰¤-ext pf2)                  x =
  ap there (compose-Ï-â‰¤ pf1 (â‰¤-ext pf2) x)
